* LOADER - FLEX LOADER ROUTINE
*
* COPYRIGHT (C) 1980 BY
* TECHNICAL SYSTEMS CONSULTANTS, INC.
* 111 PROVIDENCE RD, CHAPEL HILL, NC 27514
*
* LOADS FLEX FROM DISK. ASSUMES DRIVE IS ALREADY
* SELECTED AND A RESTORE HAS BEEN PERFORMED BY THE
* ROM BOOT AND THAT STARTING TRACK AND SECTOR OF
* FLEX ARE AT $C105 AND $C106. BEGIN EXECUTION
* BY JUMPING TO LOCATION $C100. JUMPS TO FLEX
* STARTUP WHEN COMPLETE.
*
* EQUATES
C07F STACK EQU $C07F
C300 SCTBUF EQU $C300 DATA SECTOR BUFFER
* START OF UTILITY
C100 ORG $C100
C100 20 0A LOAD BRA LOAD0
C102 00 00 00 FCB 0,0,0
C105 00 TRK FCB 0 FILE START TRACK
C106 00 SCT FCB 0 FILE START SECTOR
C107 00 DNS FCB 0 DENSITY FLAG
C108 C100 TADR FDB $C100 TRANSFER ADDRESS
C10A 0000 LADR FDB 0 LOAD ADDRESS
C10C 10CE C07F LOAD0 LDS #STACK SETUP STACK
C110 FC C105 LDD TRK SETUP STARTING TRK & SCT
C113 FD C300 STD SCTBUF
C116 108E C400 LDY #SCTBUF+256
* PERFORM ACTUAL FILE LOAD
C11A 8D 35 LOAD1 BSR GETCH GET A CHARACTER
C11C 81 02 CMPA #$02 DATA RECORD HEADER?
C11E 27 10 BEQ LOAD2 SKIP IF SO
C120 81 16 CMPA #$16 XFR ADDRESS HEADER?
C122 26 F6 BNE LOAD1 LOOP IF NEITHER
C124 8D 2B BSR GETCH GET TRANSFER ADDRESS
C126 B7 C108 STA TADR
C129 8D 26 BSR GETCH
C12B B7 C109 STA TADR+1
Page 57 - Appendix E
6809 FLEX Adaptation Guide SKELETAL FLEX LOADER
C12E 20 EA BRA LOAD1 CONTINUE LOAD
C130 8D 1F LOAD2 BSR GETCH GET LOAD ADDRESS
C132 B7 C10A STA LADR
C135 8D 1A BSR GETCH
C137 B7 C10B STA LADR+1
C13A 8D 15 BSR GETCH GET BYTE COUNT
C13C 1F 894D TAB PUT IN B
C13F 27 D9 BEQ LOAD1 LOOP IF COUNT=0
C141 BE C10A LDX LADR GET LOAD ADDRESS
C144 34 14 LOAD3 PSHS B,X
C146 8D 09 BSR GETCH GET A DATA CHARACTER
C148 35 14 PULS B,X
C14A A7 80 STA 0,X+ PUT CHARACTER
C14C 5A DECB END OF DATA IN RECORD?
C14D 26 F5 BNE LOAD3 LOOP IF NOT
C14F 20 C9 BRA LOAD1 GET ANOTHER RECORD
* GET CHARACTER ROUTINE - READS A SECTOR IF NECESSARY
C151 108C C400 GETCH CMPY #SCTBUF+256 OUT OF DATA?
C155 26 0F BNE GETCH4 GO READ CHARACTER IF NOT
C157 8E C300 GETCH2 LDX #SCTBUF POINT TO BUFFER
C15A EC 84 LDD 0,X GET FORWARD LINK
C15C 27 0B BEQ GO IF ZERO, FILE IS LOADED
C15E 8D 0D BSR READ READ NEXT SECTOR
C160 26 9E BNE LOAD START OVER IF ERROR
C162 108E C304 LDY #SCTBUF+4 POINT PAST LINK
C166 A6 A0 GETCH4 LDA 0,Y+ ELSE, GET A CHARACTER
C168 39 RTS
* FILE IS LOADED, JUMP TO IT
C169 6E 9F C108 GO JMP [TADR] JUMP TO TRANSFER ADDRESS
* READ SINGLE SECTOR
*
* THIS ROUTINE MUST READ THE SECTOR WHOSE TRACK
* AND SECTOR ADDRESS ARE IN A ANB B ON ENTRY.
* THE DATA FROM THE SECTOR IS TO BE PLACED AT
* THE ADDRESS CONTAINED IN X ON ENTRY.
* IF ERRORS, A NOT-EQUAL CONDITION SHOULD BE
* RETURNED. THIS ROUTINE WILL HAVE TO DO SEEKS.
* A,B,X, AND U MAY BE DESTROYED BY THIS ROUTINE,
* BUT Y MUST-BE PRESERVED.
C16D C6 FF READ LDB #$FF MUST BE USER SUPPLIED!
C16F 39 RTS THIS CODE DISABLES READ!
END