* NEWDISK
*
* COPYRIGHT (C) 1980 BY
* TECHNICAL SYSTEMS CONSULTANTS, INC.
* 111 PROVIDENCE RD, CHAPEL HILL, NC.27514
*
* DISK FORMATTING PROGRAM FOR 6809 FLEX.
* GENERAL VERSION DESIGNED FOR WD 1771/1791.
* THE NEWDISK PROGRAM INITIALIZES A NEW DISKETTE AND
* THEN PROCEEDS TO VERIFY ALL SECTORS AND INITIALIZE
* TABLES. THIS VERSION IS SETUP FOR AN 8 INCH DISK
* SYSTEM WITH HINTS AT CERTAIN POINTS FOR ALTERING
* FOR A SINGLE-DENSITY 5 INCH DISK SYSTEM. THIS
* VERSION IS NOT INTENDED FOR 5 INCH DOUBLE-DENSITY.
**************************************************
* DISK SIZE PARAMETERS
* **** **** **********
* THE FOLLOWING CONSTANTS SETUP THE SIZE OF THE
* DISK TO BE FORMATTED. THE VALUES SHOWN ARE FOR
* 8 INCH DISKS. FOR 5 INCH DISKS, USE APPROPRIATE
* VALUES. (IE. 35 TRACKS AND 10 SECTORS PER SIDE)
**************************************************
004D MAXTRK EQU 77 NUMBER OF TRACKS
* SINGLE DENSITY:
000F SMAXS0 EQU 15 SD MAX SIDE 0 SECTORS
001E SMAX51 EQU 30 SD MAX SIDE 1 SECTORS
* DOUBLE DENSITY:
001A DMAXS0 EQU 26 DD MAX SIDE 0 SECTORS
0034 DMAXS1 EQU 52 DD MAX SIDE 1 SECTORS
**************************************************
* MORE DISK SIZE DEPENDENT PARAMETERS
* **** **** **** ********* **********
* THE FOLLOWING VALUES ARE ALSO DEPENDENT ON THE
* SIZE OF DISK BEING FORMATTED. EACH VALUE SHOWN
* IS FOR 8 INCH WITH PROPER 5 INCH VALUES IN
* PARENTHESES.
**************************************************
* SIZE OF SINGLE DENSITY WORK BUFFER FOR ONE TRACK
13EC TKSZ EQU 5100 (USE 3050 FOR 5 INCH)
* TRACK START VALUE
0028 TST EQU 40 (USE 0 FOR 5 INCH
) * SECTOR START VALUE
0049 SST EQU 73 (USE 7 FOR 5 INCH)
* SECTOR GAP VALUE
001B GAP EQU 27 (USE 14 FOR 5 INCH)
**************************************************
Page 59 - Appendix F
6809 FLEX Adaptation Guide SKELETAL NEWDISK ROUTINE
* WORK SPACE WHERE ONE TRACK OF DATA IS SETUP
0800 WORK EQU $0800 WORK SPACE
1BEC SWKEND EQU TKSZ+WORK SINGLE DENSITY
2FD8 DWKEND EQU TKSZ*2+WORK DOUBLE DENSITY
* GENERAL EQUATES
0101 FIRST EQU $0101 FIRST USER SECTOR
001E FCS EQU 30 FCB CURRENT SECTOR
0040 FSB EQU 64 SECTOR BUFFER
0010 IRS EQU 16 INFO RECORD START
005D AVLP EQU FSB+IRS+13
0005 DIRSEC EQU 5 FIRST DIR. SECTOR
0009 RDSS EQU 9 READ SS FMS CODE
000A WTSS EQU 10 WRITE SS FMS CODE
CC0E DATE EQU $CC0E DOS DATE
* FLEX ROUTINES EQUATES
CD1E PSTRNG EQU $CD1E
CD18 PUTCHR EQU $CD18
CD39 OUTDEC EQU $CD39
CD42 GETHEX EQU $CD42
CD15 GETCHR EQU $CD15
CD24 PCRLF EQU $CD24
CD1B INBUF EQU $CD1B
CD2D GETFIL EQU $CD2D
CD48 INDEC EQU $CD48
D406 FMS EQU $D406
D403 FMSCLS EQU $D403
CD3C OUT2HS EQU $CD3C
CD03 WARMS EQU $CD03
* DISK DRIVER ROUTINES
DE03 DWRITE EQU $DE03 WRITE A SINGLE SECTOR
DE09 REST EQU $DE09 RESTORE HEAD
DE1B DSEEK EQU $DE1B SEEK TO TRACK
* TEMPORARY STORAGE
0020 ORG $0020
0020 TRACK RMB 1
0021 SECTOR RMB 1
0022 BADCNT RMB 1 BAD SECTOR COUNT
0023 DRN RMB 1
0024 SIDE RMB 1
0025 DBSDF RMB 1
0026 DENSE RMB 1
Page 60 - Appendix F
SKELETAL NEWDISK ROUTINE 6809 FLEX Adaptation Guide
0027 DNSITY RMB 1
0028 INDEX RMB 2
002A SECCNT RMB 2 SECTOR COUNTER
002C FSTAVL RMB 2 FIRST AVAILABLE
002E LSTAVL RMB 2 LAST AVAILABLE
0030 MAXS0 RMB 1 MAX SIDE 0 SECTOR
0031 MAXS1 RMB 1 MAX SIDE 1 SECTOR
0032 MAX RMB 1 MAX SECTOR
0033 FKFCB RMB 4
0037 VOLNAM RMB 11
0042 VOLNUM RMB 2
Page 61 - Appendix F
6809 FLEX Adaptation Guide SKELETAL NEWDISK ROUTINE
0100 ORG $0100
********************************************
* MAIN PROGRAM STARTS HERE
********************************************
0100 20 0C NEWDISK BRA FORM1
0102 02 VN FCB 2 VERSION NUMBER
0103 BD CD1E OUTIN JSR PSTRNG OUTPUT STRING
0106 BD CD15 OUTIN2 JSR GETCHR GET RESPONSE
0109 84 5F ANDA #$5F MAKE IT UPPER CASE
0108 81 59 CMPA #'Y SEE IF "YES"
010D 39 RTS
010E 86 0F FORM1 LDA #SMAXS0 INITIALIZE SECTOR MAX
0110 97 30 STA MAXS0
0112 97 32 STA MAX
0114 86 1E LDA #SMAXS1
0116 97 31 STA MAXS1
0118 BD CD42 JSR GETHEX GET DRIVE NUMBER
011B 1025 0080 LBCS EXIT
011F 1F 10 TFR X,D
0121 1083 0003 CMPD #3 ENSURE 0 TO 3
0125 22 78 BHI EXIT
0127 8E 0800 LDX #WORK
012A E7 03 STB 3,X
012C D7 23 STB DRN
012E 8E 04A8 LDX #SURES ASK IF HE'S SURE
0131 8D D0 BSR OUTIN PRINT & GET RESPONSE
0133 26 6A BNE EXIT EXIT IF “NO"
0135 8E 04CA LDX #SCRDS CHECK SCRATCH DRIVE NO.
0138 BD CD1E JSR PSTRNG OUTPUT IT
013B 8E 0802 LDX #WORK+2
013E 6F 84 CLR 0,X
0140 5F CLRB
0141 BD CD39 JSR OUTDEC
0144 86 3F LDA #'? PRINT QUESTION MARK
0146 BD CD18 JSR PUTCHR
0149 86 20 LDA #$20
014B BD CD18 JSR PUTCHR
014E 8D B6 BSR OUTIN2 GET RESPONSE
0150 26 4D BNE EXIT EXIT IF "NO”
0152 OF 25 CLR DBSDF CLEAR FLAG
*** PLACE A "BRA FORM25" HERE IF CONTROLLER
*** IS ONLY SINGLE SIDED.
0154 8E 0538 LDX #DBST ASK IF DOUBLE SIDED
0157 BD AA BSR OUTIN PRINT & GET RESPONSE
0159 26 06 BNE FORM25 SKIP IF "NO"
015B 0C 25 INC DBSDF SET FLAG
015D 86 1E LDA #SMAXS1 SET MAX SECTOR
015F 97 32 STA MAX
Page 62 - Appendix F
SKELETAL NEWDISK ROUTINE 6809 FLEX Adaptation Guide
0161 0F 26 FORM25 CLR DENSE INITIALIZE SINGLE DENSITY
0163 0F 27 CLR DNSITY
*** PLACE A "BRA FORM26" HERE IF CONTROLLER
*** IS ONLY SINGLE DENSITY.
0165 8E 054C LDX #DDSTR ASK IF DOUBLE DENSITY
0168 8D 99 BSR OUTIN PRINT & GET RESPONSE
016A 26 02 BNE FORM26 SKIP IF "NO"
016C 0C 26 INC DENSE SET FLAG IF SO
016E 8E 0562 FORM26 LDX #NMSTR ASK FOR VOLUME NAME
0171 BD CD1E JSR PSTRNG PRINT IT
0174 BD CD1B JSR INBUF GET LINE
0177 8E 0033 LDX #FKFCB POINT TO FAKE
017A BD CD2D JSR GETFIL
017D 8E 0570 FORM27 LDX #NUMSTR OUTPUT STRING
0180 BD CD1E JSR PSTRNG
0183 BD CD1B JSR INBUF GET LINE
0186 BD CD48 JSR INDEC GET NUMBER
0189 25 F2 BCS FORM27 ERROR?
018B 9F 42 STX VOLNUM SAVE NUMBER
018D BD CD24 JSR PCRLF PRINT CR & LF
0190 8E 0800 LDX #WORK
0193 BD DE09 JSR REST RESTORE DISK
0196 27 15 BEQ FORMAT SKIP IF NO ERROR
0198 8E 04B7 LDX #WPST
019B C5 40 BITB #$40 WRITE PROTECT ERROR?
019D 26 03 BNE EXIT2 SKIP IF 50
* EXIT ROUTINES
019F 8E 04F1 EXIT LDX #ABORTS REPORT ABORTING
01A2 BD CD1E EXIT2 JSR PSTRNG OUTPUT IT
01A5 BD D403 EXIT3 JSR FMSCLS
01A8 1C EF CLI
01AA 7E CD03 JMP WARMS RETURN
Page 63 - Appendix F
6809 FLEX Adaptation Guide SKELETAL NEWDISK ROUTINE
**************************************************************
*
* ACTUAL FORMAT ROUTINE
*
* THIS CODE PERFORMS THE ACTUAL DISK FORMATTING BY PUTTING
* ON ALL GAPS, HEADER INFORMATION, DATA AREAS, SECTOR LINKING,
* ETC. THIS SECTION DOES NOT WORRY ABOUT SETTING UP THE
* SYSTEM INFORMATION RECORD, BOOT SECTOR, OR DIRECTORY.
* IT ALSO DOES NOT NEED BE CONCERNED WITH TESTING THE DISK FOR
* ERRORS AND THE REMOVAL OF DEFECTIVE SECTORS ASSOCIATED WITH
* SUCH TESTING. THESE OPERATIONS ARE CARRIED OUT BY THE
* REMAINDER OF THE CODE IN "NEWDISK".
* IF USING A WD1771 OR WD1791 CONTROLLER CHIP, THIS CODE SHOULD
* NOT NEED CHANGING (SO LONG AS THE WRITE TRACK ROUTINE AS
* FOUND LATER IS PROVIDED). IF USING A DIFFERENT TYPE OF
* CONTROLLER, THIS CODE MUST BE REPLACED AND THE WRITE TRACK
* ROUTINE (FOUND LATER) MAY BE REMOVED AS IT WILL HAVE TO BE
* A PART OF THE CODE THAT REPLACES THIS FORMATTING CODE.
* WHEN THIS ROUTINE IS COMPLETED, IT SHOULD JUMP TO 'SETUP'.
*
**************************************************************
* MAIN FORMATTING LOOP
01AD 1A 10 FORMAT SEI
01AF 0F 20 CLR TRACK
01B1 0F 24 FORM3 CLR SIDE SET SIDE 0
01B3 0F 21 CLR SECTOR
01B5 8D 40 BSR TRKHD SETUP TRACK HEADER
01B7 8E 0849 FORM32 LDX #WORK+SST POINT TO SECTOR START
01BA 0D 27 TST DNSITY DOUBLE DENSITY?
01BC 27 03 BEQ FORM4 SKIP IF NOT
01BE 8E 0892 LDX #SST*2+WORK DD SECTOR START
01C1 BD 6E FORM4 BSR DOSEC PROCESS RAM WITH INFO
01C3 0C 21 INC SECTOR ADVANCE TO NEXT
01C5 96 21 LDA SECTOR CHECK VALUE
01C7 0D 24 TST SIDE CHECK SIDE
01C9 26 04 BNE FORM45
01CB 91 30 CMPA MAXS0
01CD 20 02 BRA FORM46
01CF 91 31 FORM45 CMPA MAXS1
01D1 26 EE FORM46 BNE FORM4 REPEAT?
01D3 96 20 FORM47 LDA TRACK GET TRACK NUMBER
01D5 D6 24 LDB SIDE FAKE SECTOR FOR PROPER SIDE
01D7 BD DE1B JSR DSEEK SEEK TRACK AND SIDE
01DA BD 0580 JSR WRTTRK WRITE TRACK
01DD 0D 25 FORM5 TST DBSDF ONE SIDE?
01DF 27 08 BEQ FORM6
01E1 00 24 TST SIDE
01E3 26 04 BNE FORM6
01E5 03 24 COM SIDE SET SIDE 1
01E7 20 CE BRA FORM32
01E9 0C 20 FORM6 INC TRACK BUMP TRACK
01EB BD 02FF JSR SWITCH SWITCH TO DD IF NCSSRY
Page 64 - Appendix F
SKELETAL NEWDISK ROUTINE 6809 FLEX Adaptation Guide
01EE 96 20 FORM7 LDA TRACK CHECK VALUE
01F0 81 4D CMPA #MAXTRK
01F2 26 BD BNE FORM3
01F4 16 00BE LBRA SETUP DONE...GO FINISH UP
* SETUP TRACK HEADER INFORMATION
01F7 BE 0800 TRKHD LDX #WORK POINT TO BUFFER
01FA 0D 27 TST DNSITY DOUBLE DENSITY?
01FC 26 11 BNE TRHDD SKIP IF SO
01FE C6 FF LDB #$FF
0200 E7 80 TRHDS1 STB 0,X+ INITIALIZE TO $FF
0202 8C 1BEC CMPX #SWKEND
0205 26 F9 BNE TRHDS1
0207 8E 0828 LDX #WORK+TST
020A 4F CLRA SET IN ZEROS
020B C6 06 LDB #6
020D 20 15 BRA TRHDD2
020F C6 4E TRHDD LDB #$4E
0211 E7 80 TRHDD1 STB 0,X+ INITIALIZE TO $4E
0213 8C 2FD8 CMPX #DWKEND
0216 26 F9 BNE TRHDDI
0218 8E 0850 LDX #TST*2+WORK
021B 4F CLRA SET IN ZEROS
021C C6 0C LDB #12
021E BD 0B BSR SET
0220 86 F6 LDA #$F6 SET IN $F6'S
0222 C6 03 LDB #3
0224 8D 05 TRHDD2 BSR SET
0226 86 FC LDA #$FC SET INDEX MARK
0228 A7 84 STA 0,X
022A 39 RTS
* SET (B) BYTES OF MEMORY TO (A) STARTING AT (X)
022B A7 80 SET STA 0,X+
022D 5A DECB
022E 26 FB BNE SET
0230 39 RTS
* PROCESS SECTOR IN RAM
0231 4F DOSEC CLRA
0232 0D 27 TST DNSITY DOUBLE DENSITY?
0234 26 04 BNE DOSEC1 SKIP IF SO
0236 C6 06 LDB #6 CLEAR 6 BYTES
0238 20 08 BRA DOSEC2
023A C6 0C DOSEC1 LDB #12 CLEAR 12 BYTES
023C 8D ED BSR SET
023E 86 F5 LDA #$F5 SET IN 3 $F5'S
0240 C6 03 LDB #3
0242 8D E7 DOSEC2 BSR SET
0244 86 FE LDA #$FE ID ADDRESS MARK
0246 A7 80 STA 0,X+
0248 96 20 LDA TRACK GET TRACK NO.
Page 65 - Appendix F
6809 FLEX Adaptation Guide SKELETAL NEWDISK ROUTINE
024A A7 80 STA 0,X+
024C D6 27 LDB DNSITY DOUBLE DENSITY?
024E 27 04 BEQ DOSEC3 SKIP IF NOT
0250 D6 24 LDB SIDE GET SIDE INDICATOR
0252 C4 01 ANDB #$01 MAKE IT 0 OR 1
0254 E7 80 DOSEC3 STB 0,X+
0256 D6 21 LDB SECTOR GET SECTOR NO.
0258 108E 0456 LDY #SSCMAP POINT TO CORRECT MAP
025C 0D 27 TST DNSITY
025E 27 04 BEQ DOSEC4
0260 108E 0474 LDY #DSCMAP
0264 E6 A5 DOSEC4 LDB B,Y GET ACTUAL SECTOR
0266 E7 80 STB 0,X+
0268 D1 32 CMPB MAX END OF TRACK?
026A 26 09 DOSEC6 BNE DOSEC7 SKIP IF NOT
026C 4C INCA BUMP TRACK NO.
026D 5F CLRB RESET SECTOR NO.
026E 81 4D CMPA #MAXTRK END OF DISK?
0270 26 03 BNE DOSEC7 SKIP IF NOT
0272 4F CLRA SET ZERO FORWARK LINK
0273 C6 FF LDB #-1
0275 5C DOSEC7 INCB BUMP SECTOR NO.
0276 34 06 PSHS D SAVE FORWARD LINK
0278 86 01 LDA #1 SECTOR LENGTH = 256
027A A7 80 STA 0,X+
027C 86 F7 LDA #$F7 SET CRC CODE
027E A7 80 STA 0,X+
0280 0D 27 TST DNSITY DOUBLE DENSITY?
0282 26 07 BNE DOSEC8 SKIP IF S0
0284 30 0B LEAX 11,X LEAVE $FF'S
0286 4F CLRA PUT IN 6 ZEROS
0287 C6 06 LDB #6
0289 20 0C BRA DOSEC9
028B 30 88 16 DOSEC8 LEAX 22,X LEAVE $4E'S
028E 4F CLRA PUT IN 12 ZEROS
028F C6 0C LDB #12
0291 8D 98 BSR SET
0293 86 F5 LDA #$F5 PUT IN 3 $F5'S
0295 C6 03 LDB #3
0297 8D 92 DOSEC9 BSR SET
0299 86 FB LDA #$FB DATA ADDRESS MARK
029B A7 80 STA 0,X+
029D 35 06 PULS D RESTORE FORWARD LINK
029F ED 81 STD 0,X++ PUT IN SECTOR BUFFER
02A1 4F CLRA CLEAR SECTOR BUFFER
02A2 C6 FE LDB #254
02A4 8D 85 BSR SET
02A6 86 F7 LDA #$F7 SET CRC CODE
02A8 A7 80 STA 0,X+
02AA 30 88 1B LEAX GAP,X LEAVE GAP
02AD 0D 27 TST DNSITY DOUBLE DENSITY?
02AF 27 03 BEQ DOSECA SKIP IF NOT
02B1 30 88 1B LEAX GAP,X DD NEEDS MORE GAP
02B4 39 DOSECA RTS
Page 66 - Appendix F
SKELETAL NEWDISK ROUTINE 6809 FLEX Adaptation Guide
**************************************************************
*
* DISK TESTING AND TABLE SETUP
*
* THE FOLLOWING CODE TESTS EVERY SECTOR AND REMOVES ANY
* DEFECTIVE SECTORS FROM THE FREE CHAIN. NEXT THE SYSTEM
* INFORMATION RECORD IS SETUP, THE DIRECTORY IS INITIALIZED,
* AND THE BOOT IS SAVED ON TRACK ZERO. ALL THIS CODE SHOULD
* WORK AS IS FOR ANY FLOPPY DISK SYSTEM. ONE CHANGE THAT
* MIGHT BE REQUIRED WOULD BE IN THE SAVING OF THE BOOTSTRAP
* LOADER. SPECIAL BOOT LOADERS MIGHT REQUIRE CHANGES IN THE
* WAY THE BOOT SAVE IS PERFORMED. FOR EXAMPLE, IT MAY BE
* NECESSARY TO SAVE TWO SECTORS IF THE BOOT LOADER DOES NOT
* FIT IN ONE. ALSO IT MAY BE NECESSARY, BY SOME MEANS, TO
* INFORM THE BOOT LOADER WHETHER THE DISK IS SINGLE OR
* DOUBLE DENSITY SO THAT IT MAY SELECT THE PROPER DENSITY
* FOR LOADING FLEX.
*
* **************************************************************
* READ ALL SECTORS FOR ERRORS
02B5 D6 32 SETUP LDB MAX GET MAX SECTORS
02B7 86 4C LDA #MAXTRK-1
02B9 DD 2E STD LSTAVL SET LAST AVAIL.
02BB 3D MUL FIND TOTAL SECTORS
02BC DD 2A STD SECCNT SAVE IT
02BE 8E 0101 LDX #FIRST SET FIRST AVAIL
02C1 9F 2C STX FSTAVL
02C3 96 23 LDA DRN
02C5 B7 0803 STA WORK+3
02C8 4F CLRA CLEAR COUNTER
02C9 97 22 STA BADCNT
02CB 97 20 STA TRACK SET TRACK
02CD 97 27 STA DNSITY SNGL DNST FOR TRK 0
02CF 4C INCA
02D0 97 21 STA SECTOR SET SECTOR
02D2 86 0F LDA #SMAXS0 RESET MAXIMUM
02D4 97 30 STA MAXS0 SECTOR COUNTS
02D6 86 1E LDA #SMAXS1
02D8 97 31 STA MAXS1
02DA 0D 25 TST DBSDF DOUBLE SIDED?
02DC 26 02 BNE SETUP1 SKIP IF SO
02DE 86 0F LDA #SMAXS0
02E0 97 32 SETUP1 STA MAX SET MAXIMUM SECTORS
02E2 8D 10 SETUP2 BSR CHKSEC GO CHECK SECTOR
02E4 26 3C BNE REMSEC ERROR?
02E6 0F 22 CLR BADCNT CLEAR COUNTER
02E8 DC 20 SETUP4 LDD TRACK GET TRACK & SECTOR
02EA 8D 2A BSR FIXSEC GET TO NEXT ADR
02EC 1027 00B3 LBEQ DOTRK SKIP IF FINISHED
02F0 DD 20 STD TRACK SET TRACK & SECTOR
02F2 20 EE BRA SETUP2 REPEAT
Page 67 - Appendix F
6809 FLEX Adaptation Guide SKELETAL NEWDISK ROUTINE
* CHECK IF SECTOR GOOD
02F4 8E 0800 CHKSEC LDX #WORK POINT TO FCB
02F7 DC 20 LDD TRACK GET TRACK & SECTOR
02F9 ED 88 1E STD FCS,X SET CURRENT TRK & SCT
02FC 7E 038C JMP READSS GO DO READ
* SWITCH TO DOUBLE DENSITY IF NECESSARY
02FF D6 26 SWITCH LDB DENSE DOUBLE DENSITY DISK?
0301 27 12 BEQ SWTCH2 SKIP IF NOT
0303 D7 27 STB DNSITY SET FLAG
0305 C6 1A LDB KDMAXS0 RESET SECTOR COUNTS
0307 D7 30 STB MAXS0
0309 C6 34 LDB #DMAXS1
030B D7 31 STB MAXS1
030D 0D 25 TST DBSDF DOUBLE SIDED?
030F 26 02 BNE SWTCH1 SKIP IF SO
0311 C6 1A LDB #DMAXS0
0313 D7 32 SWTCH1 STB MAX SET MAX SECTOR
0315 39 SWTCH2 RTS
* SET TRK & SEC TO NEXT
0316 D1 32 FIXSEC CMPB MAX END OF TRACK?
0318 26 04 BNE FIXSE4 SKIP IF NOT
031A 4C INCA BUMP TRACK
031B 8D E2 BSR SWITCH SWITCH TO DD IF NCSSRY
031D 5F CLRB RESET SECTOR NO.
031E 5C FIXSE4 INCB BUMP SECTOR NO.
031F 81 4D CMPA #MAXTRK END OF DISK?
0321 39 RTS
* REMOVE BAD SECTOR FROM FREE SECTOR CHAIN
0322 0C 22 REMSEC INC BADCNT UPDATE COUNTER
0324 27 0A BEQ REMSE1 COUNT OVERFLOW?
0326 96 20 LDA TRACK GET TRACK
0328 26 0C BNE REMSE2 TRACK 0?
032A D6 21 LDB SECTOR GET SECTOR
032C C1 05 CMPB #DIRSEC PAST DIRECTORY?
032E 22 06 BHI REMSE2
0330 8E 04E1 REMSE1 LDX #FATERS REPORT FATAL ERROR
0333 7E 01A2 JMP EXIT2 REPORT IT
0336 8E 0800 REMSE2 LDX #WORK POINT TO FCB
0339 DC 2C LDD FSTAVL GET 1ST TRACK & SECTOR
033B 1093 20 CMPD TRACK CHECK TRACK & SECTOR
033E 26 06 BNE REMSE3
0340 8D D4 BSR FIXSEC SET TO NEXT
0342 DD 2C STD FSTAVL SET NEW ADR
0344 20 27 BRA REMSE8 GO DO NEXT
0346 DC 20 REMSE3 LDD TRACK GET TRACK & SECTOR
0348 D0 22 SUBB BADCNT
Page 68 - Appendix F
SKELETAL NEWDISK ROUTINE 6809 FLEX Adaptation Guide
034A 27 02 BEQ REMS35 UNDERFLOW?
034C 2A 03 BPL REMSE4
034E 4A REMS35 DECA DEC TRACK
034F D6 32 LDB MAX RESET SECTOR
0351 ED 88 1E REMSE4 STD FCS,X SET CURRENT ADR
0354 8D 36 BSR READSS GO DO READ
0356 26 D8 BNE REMSE1 ERROR?
0358 EC 88 40 LDD FSB,X GET LINK ADR
035B 8D B9 BSR FIXSEC POINT TO NEXT
035D 26 07 BNE REMSE6 OVERFLOW?
035F EC 88 1E LDD FCS,X GET CURRENT ADR
0362 DD 2E STD LSTAVL SET NEW LAST AVAIL
0364 4F CLRA SET END LINK
0365 5F CLRB
0366 ED 88 40 REMSE6 STD FSB,X SET NEW LINK
0369 8D 2B BSR WRITSS GO DO WRITE
036B 26 C3 BNE REMSE1 ERROR?
036D 9E 2A REMSE8 LDX SECCNT GET SEC COUNT
036F 30 1F LEAX -1,X DEC IT ONCE
0371 9F 2A STX SECCNT SAVE NEW COUNT
0373 8E 0504 LDX #BADSS REPORT BAD SECTOR
0376 BD CD1E JSR PSTRNG OUTPUT IT
0379 BE 0020 LDX #TRACK POINT TO ADDRESS
037C BD CD3C JSR OUT2HS OUTPUT IT
037F 86 20 LDA #$20
0381 BD CD18 JSR PUTCHR
0384 30 01 LEAX 1,X BUMP TO NEXT
0386 BD CD3C JSR OUT2HS
0389 7E 02E8 JMP SETUP4 CONTINUE
* READ A SECTOR
038C 8E 0800 READSS LDX #WORK POINT TO FCB
038F 86 09 LDA #RDSS SET UP COMMAND
0391 A7 84 STA 0,X
0393 7E D406 JMP FMS GO DO IT
* WRITE A SECTOR
0396 BE 0800 WRITSS LDX #WORK POINT TO FCB
0399 86 0A LDA #WTSS SETUP COMMAND
039B A7 84 STA 0,X
039D BD D406 JSR FMS GO DO IT
03A0 27 EA BEQ REABSS ERRORS?
03A2 39 RTS ERROR RETURN
* SETUP SYSTEM INFORMATION RECORD
03A3 0F 27 DOTRK CLR DNSITY BACK TO SINGLE DENSITY
03A5 8E 0800 LDX #WORK POINT TO SPACE
03A8 6F 88 1E CLR FCS,X SET TO DIS
03AB 86 03 LDA #3 SECTOR 3
03AD A7 88 1F STA FCS+1,X
03B0 80 DA BSR READSS READ IN SIR SECTOR
Page 69 - Appendix F
6809 FLEX Adaptation Guide SKELETAL NEWDISK ROUTINE
03B2 26 48 BNE DOTRK4 ERROR?
03B4 8E 0800 LDX #WORK FIX POINTER
03B7 6F 88 40 CLR FSB,X CLEAR FORWARD LINK
03BA 6F 88 41 CLR FSB+1,X
03BD DC 2C LDD FSTAVL ADDR. OF 1ST FREE SCTR.
03BF ED 88 5D STD AVLP,X SET IN SIR
03C2 DC 2E LDD LSTAVL ADDR. OF LAST FREE SCTR.
03C4 ED 88 5F STD AVLP+2,X PUT IN SIR
03C7 DC 2A LDD SECCNT GET TOTAL SECTOR COUNT
03C9 ED 88 61 STD AVLP+4,X PUT IN SIR
03CC 86 4C LDA #MAXTRK-1 GET MAX TRACK NO.
03CE D6 30 LDB MAXS0 SET MAX SECTORS/TRACK
03D0 0D 25 TST DBSDF DOUBLE SIDE?
03D2 27 02 BEQ DOTRK2
03D4 D6 31 LDB MAXS1. CHANGE FOR DOUBLE SIDED
03D6 ED 88 66 DOTRK2 STD AVLP+9,X SAVE MAX TRACK & SECTOR
03D9 FC CC0E LDD DATE SET DATE INTO SIR
03DC ED 88 63 STD AVLP+6,X
03DF B6 CC10 LDA DATE+2
03E2 A7 88 65 STA AVLP+8,X
03E5 C6 0D LDB #13
03E7 108E 0037 LDY #VOLNAM POINT TO VOLUME NAME
03EB 8E 0800 LDX #WORK
03EE A6 A0 DOTR33 LDA 0,Y+ COPY NAME TO SIR
03F0 A7 88 50 STA FSB+IRS,X
03F3 30 01 LEAX 1,X
03F5 5A DECB DEC THE COUNT
03F6 26 F6 BNE DOTR33
03F8 8D 9C BSR WRITSS WRITE SIR BACK OUT
03FA 27 03 BEQ DIRINT SKIP IF NO ERROR
03FC 7E 0330 DOTRK4 JMP REMSE1 GO REPORT ERROR
* INITIALIZE DIRECTORY
03FF 8E 0800 DIRINT LDX #WORK SET POINTER
0402 86 0F LDA #SMAXS0 GET MAX FOR TRK 0
0404 0D 25 TST DBSDF SINGLE SIDE?
0406 27 02 BEQ DIRIN1 SKIP IF SO
0408 86 1E LDA #SMAXS1 SET MAX FOR DS
040A A7 88 1F DIRIN1 STA FCS+1,X SET UP
040D BD 038C JSR READSS READ IN SECTOR
0410 26 EA BNE DOTRK4 ERROR?
0412 8E 0800 LDX #WORK RESTORE POINTER
0415 6F 88 40 CLR FSB,X CLEAR LINK
0418 6F 88 41 CLR FSB+1,X
041B BD 0396 JSR WRITSS WRITE BACK OUT
041E 26 DC BNE DOTRK4 ERRORS?
Page 70 - Appendix F
SKELETAL NEWDISK ROUTINE 6809 FLEX Adaptation Guide
* SAVE BOOT ON TRACK 0 SECTOR 1
* (MAY REQUIRE CHANGES - SEE TEXT ABOVE)
0420 8E C100 DOBOOT LDX #BOOT POINT TO LOADER CODE
0423 4F CLRA TRACK #0
0424 C6 01 LDB #1 SECTOR #1
0426 BD DE03 JSR DWRITE WRITE THE SECTOR
0429 26 D1 BNE DOTRK4 SKIP IF AN ERROR
* REPORT TOTAL SECTORS AND EXIT
042B 8E 0800 LDX #WORK SETUP AN FCB
042E 86 10 LDA #16 OPEN SIR FUNCTION
0430 A7 84 STA 0,X
0432 BD D406 JSR FMS OPEN THE SIR
0435 26 C5 BNE DOTRK4
0437 86 07 LDA #7 GET INFO RECORD FUNCTION
0439 A7 84 STA 0,X
043B BD D406 JSR FMS GET 1ST INFO RECORD
043E 26 BC BNE DOTRK4
0440 8E 0513 LDX #CMPLTE REPORT FORMATTING COMPLETE
0443 BD CD1E JSR PSTRNG
0446 8E 0527 LDX #SECST PRINT TOTAL SECTORS STRING
0449 BD CD1E JSR PSTRNG
044C 8E 0815 LDX #WORK+21 TOTAL IS IN INFO RECORD
044F 5F CLRB
0450 BD CD39 JSR OUTDEC PRINT NUMBER
0453 7E 01A5 JMP EXIT3 ALL FINISHED!
Page 71 - Appendix F
6809 FLEX Adaptation Guide SKELETAL NEWDISK ROUTINE
**************************************************
* SECTOR MAPS
* ****** ****
* THE MAPS SHOWN BELOW CONTAIN THE CORRECT
* INTERLEAVING FOR AN 8 INCH DISK. IF USING 5
* INCH DISKS (SINGLE DENSITY) YOU SHOULD USE
* SOMETHING LIKE '1,3,5,7,9,2,4,6,8,10' FOR
* SSCMAP FOR A SINGLE SIDED DISK.
**************************************************
0456 01 06 0B 03 SSCMAP FCB 1,6,11,3,8,13,5,10
045E 0F 02 07 0C FCB 15,2,7,12,4,9,14
0465 10 15 1A 12 FCB 16,21,26,18,23,28,20,25
046D 1E 11 16 1B FCB 30,17,22,27,19,24,29
0474 01 0E 03 10 DSCMAP FCB 1,14,3,16,5,18,7
047B 14 09 16 0B FCB 20,9,22,11,24,13
0481 1A 02 0F 04 FCB 26,2,15,4,17,6,19
0488 08 15 0A 17 FCB 8,21,10,23,12,25
048E 1B 28 1D 2A FCB 27,40,29,42,31,44,33
0495 2E 23 30 25 FCB 46,35,48,37,50,39
049B 34 1C 29 1E FCB 52,28,41,30,43,32,45
04A2 22 2F 24 31 FCB 34,47,36,49,38,51
* STRINGS
04A8 41 52 45 20 SURES FCC 'ARE YOU SURE? '
04B6 04 FCB 4
04B7 44 49 53 4B WPST FCC 'DISK IS PROTECTED!'
04C9 04 FCB 4
04CA 53 43 52 41 SCRDS FCC 'SCRATCH DISK IN DRIVE '
04E0 04 FCB 4
04E1 46 41 54 41 FATERS FCC 'FATAL ERROR --- '
04F1 46 4F 52 4D ABORTS FCC 'FORMATTING ABORTED'
0503 04 FCB 4
0504 42 41 44 20 BADSS FCC 'BAD SECTOR AT '
0512 04 FCB 4
0513 46 4F 52 4D CMPLTE FCC 'FORMATTING COMPLETE'
0526 04 FCB 4
0527 54 4F 54 41 SECST FCC 'TOTAL SECTORS '
0537 04 FCB 4
0538 44 4F 55 42 DBST FCC 'DOUBLE SIDED DISK? '
054B 04 FCB 4
054C 44 4F 55 42 DDSTR FCC 'DOUBLE DENSITY DISK? '
0561 04 FCB 4
0562 56 4F 4C 55 NMSTR FCC 'VOLUME NAME? '
056F 04 FCB 4
0570 56 4F 4C 55 NUMSTR FCC 'VOLUME NUMBER? '
057F 04 FCB 4
Page 72 - Appendix F
SKELETAL NEWDISK ROUTINE 6809 FLEX Adaptation Guide
***************************************************************
* WRITE TRACK ROUTINE *
***************************************************************
* THIS SUBROUTINE MUST BE USER SUPPLIED. *
* IT SIMPLY WRITES THE DATA FOUND AT "WORK" ($0800) TO THE *
* CURRENT TRACK ON THE DISK. NOTE THAT THE SEEK TO TRACK *
* OPERATION HAS ALREADY BEEN PERFORMED. IF SINGLE DENSITY, *
* "TKSZ" BYTES SHOULD BE WRITTEN. IF DOUBLE, "TKSZ*2" *
* BYTES SHOULD BE WRITTEN. THIS ROUTINE SHOULD PERFORM *
* ANY NECESSARY DENSITY SELECTION BEFORE WRITING. DOUBLE *
* DENSITY IS INDICATED BY THE BYTE "DNSITY" BEING NON-ZERO. *
* THERE ARE NO ENTRY PARAMETERS AND ALL REGISTERS MAY BE *
* DESTROYED ON EXIT. THE CODE FOR THIS ROUTINE MUST NOT *
* EXTEND PAST $0800 SINCE THE TRACK DATA IS STORED THERE. *
***************************************************************
*********************************************
* WESTERN DIGITAL PARAMTERS
* ******* ******* *********
* REGISTERS:
0000 COMREG EQU $0000 COMMAND REGISTER
0000 TRKREG EQU $0000 TRACK REGISTER
0000 SECREG EQU $0000 SECTOR REGISTER
0000 DATREG EQU $0000 DATA REGISTER
* COMMANDS:
00F4 WTCMD EQU $F4 WRITE TRACK COMMAND
*********************************************
*********************************************
* CONTROLLER DEPENDENT PARAMETERS
* ********** ********* **********
0000 DRVREG EQU $0000 DRIVE SELECT REGISTER
*********************************************
0580 12 WRTTRK NOP ROUTINE GOES HERE
0581 39 RTS
Page 73 - Appendix F
6809 FLEX Adaptation Guide SKELETAL NEWDISK ROUTINE
**********************************************************
*
* BOOTSTRAP FLEX LOADER
*
* THE CODE FOR THE BOOTSTRAP FLEX LOADER MUST BE IN MEMORY
* AT $C100 WHEN NEWDISK IS RUN. THERE ARE TWO WAYS IT CAN
* BE PLACED THERE. ONE IS TO ASSEMBLE THE LOADER AS A
* SEPARATE FILE AND APPEND IT ONTO THE END OF THE NEWDISK
* FILE. THE SECOND IS TO SIMPLY PUT THE SOURCE FOR THE
* LOADER IN-LINE HERE WITH AN ORG TO $C100. THE FIRST FEW
* LINES OF CODE FOR THE LATTER METHOD ARE GIVEN HERE TO
* GIVE THE USER AN IDEA OF HOW TO SETUP THE LOADER SOURCE.
*
* IT IS NOT NECESSARY TO HAVE THE LOADER AT $C100 IN ORDER
* FOR THE NEWDISK TO RUN. IT SIMPLY MEANS THAT WHATEVER
* HAPPENS TO BE IN MEMORY AT $C100 WHEN NEWDISK IS RUN
* WOULD BE WRITTEN OUT AS A BOOT. AS LONG AS THE CREATED
* DISK WAS FOR USE AS A DATA DISK ONLY AND WOULD NOT BE
* BOOTED FROM, THERE WOULD BE NO PROBLEM.
*
**********************************************************
* 6809 BOOTSTRAP FLEX LOADER
C100 ORG $C100
C100 20 07 BOOT BRA BOOT1
C102 00 00 00 FCB 0,0,0
C105 00 TRK FCB 0 STARTING TRACK.(AT $C105)
C106 00 SCTR FCB 0 STARTING SECTUR (AT $C106)
C107 0000 TEMP FDB 0
C300 FCB EQU $C300
C109 7E C109 BOOT1 JMP BOOT1 ROUTINE GOES HERE
**********************************************************
END NEWDISK
Page 74 -