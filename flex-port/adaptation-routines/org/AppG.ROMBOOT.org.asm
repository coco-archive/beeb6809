	* ROM BOOT FOR SWTPC 6809 MF-68
	*
	* COPYRIGHT (C) 1980 BY
	* TECHNICAL SYSTEMS CONSULTANTS. INC.
	* 111 PROVIDENCE RD, CHAPEL HILL, NC 27514
	* EQUATES
DRVREG		EQU	$E014
COMREG		EQU	$E018
SECREG		EQU	$E01A
DATREG		EQU	$E01B
LOADER		EQU	$C100
		ORG	$7000
START		LDA	COMREG				; TURN MOTOR ON
		CLR	DRVREG				; SELECT DRIVE #0
		LDX	#0000
OVR		LEAX	-1,X				; DELAY FOR MOTOR SPEEDUP
		BNE	OVR
		LDB	#$0F				; DO RESTORE COMMAND
		STB	COMREG
		BSR	DELAY
LOOP1		LDB	COMREG				; CHECK WD STATUS
		BITB	#1				; WAIT TIL NOT BUSY
		BNE	LOOP1
		LDA	#1				; SETUP FOR SECTOR #1
		STA	SECREG
		BSR	DELAY
		LDB	#$8C				; SETUP READ COMMAND
		STB	COMREG
		BSR	DELAY
		LDX	#LOADER				; ADDRESS OF LOADER
LOOP2		BITB	#2				; DATA PRESENT?
		BEQ	LOOP3				; SKIP IF NOT
		LDA	DATREG				; GET A BYTE
		STA	0,X+				; PUT IN MEMORY
LOOP3		LDB	COMREG				; CHECK WD STATUS
		BITB	#1				; IS WD BUSY?
		BNE	LOOP2				; LOOP IF SO
		JMP	LOADER				; JUMP TO FLEX LOADER
DELAY		BSR	RTN
RTN		RTS
error ;;;; !!!! ;;;; END START
