* TEST UTILITY
*
*
*
*
* TESTS SINGLE SECTOR READ AND WRITE ROUTINES.
* PROGRAM PROMPTS USER FOR FUNCTION (F?) TO WHICH THE
* USER CAN RESPOND 'R' (READ) OR #W@ (WRITE). THEN IT
* PROMPTS FOR SINGLE DIGIT DRIVE NUMBER (D?), TWO DIGIT
* HEX TRACK NUMBER (T?) AND TWO DIGIT HEX SECTOR
* NUMBER (S?). AFTER PERFORMING THE FUNCTION, TEST
* REPEATS THE PROMPTING FOR ANOTHER FUNCTION.
*
* ASSUMES THE CONSOLE I/O PACKAGE DRIVERS ARE RESIDENT.
* BEGIN EXECUTION BY JUMPING TO $0100.
*
* EQUATES
D3FB INCH EQU $D3FB
D3F9 OUTCH EQU $D3F9
D3F5 TINIT EQU $D3F5
D3F3 MONITR EQU $D3F3
C07F STACK EQU $C07F
C840 FCB EQU $C840
1000 BUFFER EQU $1000
DE00 READ EQU $DE00
DE03 WRITE EQU $DE03
DE0C DRIVE EQU $DE0C
* TEMPORARY STORAGE
0020 ORG $0020
0020 COMMND RMB 1
0021 TRACK RMB 1
0022 SECTOR RMB 1
* START OF PROGRAM
0100 ORG $0100
0100 10CE C07F TEST LDS #STACK SETUP STACK
0104 AD 9F D3F5 JSR [TINIT] INITIALIZE TERMINAL
Page 51 - Appendix C
COPYRIGHT Â© 1980 by
Technical Systems Consultants, Inc.
111 Providence Road
Chapel Hill, North Carolina 27514
6809 FLEX Adaptation Guide SECTOR READ/WRITE TEST
* GET COMMAND
0108 10CE C07F TEST1 LDS #STACK RESET STACK
010C BD 58 BSR PCRLF
010E 86 46 LDA #'F PROMPT FOR FUNCTION
0110 BD 4A BSR PROMPT
0112 BD 5F BSR INPUT GET RESPONSE
0114 81 52 CMPA #'R READ COMMAND?
0116 27 08 BEQ TEST2
0118 81 57 CMPA #'W WRITE COMMAND?
011A 27 04 BEQ TEST2
011C 6E 9F D3F3 JMP [MONITR] EXIT THE PROGRAM
0120 97 20 TEST2 STA COMMND SAVE COMMAND
0122 86 44 LDA #'D PROMPT FOR DRIVE
0124 BD 36 BSR PROMPT
0126 BD 01C9 JSR INHEX GET RESPONSE
0129 81 04 CMPA #4 ENSURE 0 TO 3
012B 24 DB BHS TEST1
012D B7 C843 STA FCB+3 SAVE IT
0130 86 54 LDA #'T PROMPT FOR TRACK
0132 BD 2E BSR HPRMPT GET HEX PROMPT
0134 97 21 STA TRACK
0136 86 53 LDA #'S PROMPT FOR SECTOR
0138 8D 28 BSR HPRMPT GET HEX RESPONSE
013A 97 22 STA SECTOR SAVE IT
013C BD 28 BSR PCRLF DO LINE FEED
* GOT COMMAND, NOW DO IT
013E 96 20 LDA COMMND GET COMMAND
0140 81 57 CMPA #'W A WRITE COMMAND?
0142 26 52 BNE DOREAD IF NOT, ITS A READ
0144 8D 37 BSR SELECT SELECT DRIVE
0146 BE 1000 LDX #BUFFER POINT TO BUFFER
0149 DC 21 LDD TRACK POINT TO TRACK & SECTOR
014B BD DE03 JSR WRITE WRITE THE DATA
014E 26 35 BNE ERROR
0150 BD 14 BSR PCRLF
0152 86 4F LDA #'O PRINT OK
0154 8D 23 BSR OUTPUT
0156 86 4B LDA #'K
0158 BD 1F BSR OUTPUT
015A 20 AC BRA TEST1 DO AGAIN
* PROMPT ROUTINES
015C BD 08 PROMPT BSR PCRLF DO LINE FEED
015E 8D 19 BSR OUTPUT OUTPUT PROMPT LETTER
0160 20 15 BRA QUEST PRINT QUESTION MARK
0162 BD F8 HPRMPT BSR PROMPT DO PROMPT
0164 20 56 BRA INBYTE GET HEX BYTE
Page 52 - Appendix C
SECTOR READ/WRITE TEST 6809 FLEX Adaptation Guide
* CARRIAGE RETURN LINE FEED ROUTINE
0166 34 02 PCRLF PSHS A SAVE A
0168 86 0D LDA #$0D RETURN
016A 8D 0D BSR OUTPUT
016C 86 0A LDA #$0A LINE FEED
016E 8D 09 BSR OUTPUT
0170 35 02 PULS A RESTORE A
0172 39 RET RTS
* I/O ROUTINES
0173 6E 9F D3FB INPUT JMP [INCH]
0177 86 3F QUEST LDA #'?
0179 6E 9F D3F9 OUTPUT JMP [OUTCH]
* DRIVE SELECT ROUTINE
017D 8E C840 SELECT LDX #FCB
0180 BD DE0C JSR DRIVE
0183 27 ED BEQ RET RETURN IF NO ERROR
* DRIVER ERROR
0185 8D 0F ERROR BSR PCRLF
0187 86 45 LDA #'E
0189 8D EE BSR OUTPUT
018B 86 3D LDA #'=
018D 8D EA BSR OUTPUT
018F 1F 98 TFR B,A GET ERROR CODE
0191 8D 4D BSR OUTHEX
0193 16 FF72 LBRA TEST1 START OVER
* DO SINGLE SECTOR READ
0196 8D E5 DOREAD BSR SELECT SELECT DRIVE
0198 8E 1000 LDX #BUFFER POINT TO BUFFER
019B DC 21 LDD TRACK POINT TO TRACK & SECTOR
019D BD DE00 JSR READ READ THE DATA
01A0 26 E3 BNE ERROR
* DUMP DATA TO CONSOLE
01A2 8E 1000 LDX #BUFFER
01A5 86 10 LDA #16 NO OF LINES
01A7 34 02 DUMPI PSHS A SAVE NO OF LINES
01A9 8D BB BSR PCRLF
01AB C6 10 LDB #16 NO OF BYTES
01AD A6 80 DUMP2 LDA 0,X+ GET A BYTE
01AF 8D 2F BSR OUTHEX OUTPUT IT
01B1 5A DECB DONE WITH LINE?
01B2 26 F9 BNE DUMP2
01B4 35 02 PULS A GET NO OF LINES
01B6 4A DECA DONE WITH DUMP
Page 53 - Appendix C
6809 FLEX Adaptation Guide SECTOR READ/WRITE TEST
01B7 26 EE BNE DUMP1 LOOP IF NOT
01B9 16 FF4C LBRA TEST1 GET NEXT COMMAND
* INPUT HEX BYTE ROUTINE
01BC 8D 0B INBYTE BSR INHEX
01BE 48 ASLA
01BF 48 ASLA
01C0 48 ASLA
01C1 48 ASLA
01C2 34 02 PSHS A
01C4 8D 03 BSR INHEX
01C6 AB E0 ADDA 0,S+
01C8 39 RETN RTS
01C9 8D A8 INHEX BSR INPUT
01CB 80 47 SUBA #$47
01CD 2A 0C BPL INERR
01CF 8B 06 ADDA #6
01D1 2A 04 BPL INH2
01D3 8B 07 ADDA #7
01D5 2A 04 BPL INERR
01D7 8B 0A INH2 ADDA #10
01D9 2A ED BPL RETN
01DB 8D 9A INERR BSR QUEST PRINT A QUESTION MARK
01DD 16 FF28 LBRA TEST1 GO START OVER
* OUTPUT HEX BYTE (FOLLOWED BY SPACE)
01E0 34 02 OUTHEX PSHS A
01E2 44 LSRA
01E3 44 LSRA
01E4 44 LSRA
01E5 44 LSRA
01E6 8D 08 BSR OUTHR
01E8 35 02 PULS A
01EA 8D 04 BSR OUTHR
01EC 86 20 LDA #$20
01EE 20 89 BRA OUTPUT
01F0 84 0F OUTHR ANDA #$0F
01F2 8B 90 ADDA #$90
01F4 19 DAA
01F5 89 40 ADCA #$40
01F7 19 DAA
01F8 16 FF7E LBRA OUTPUT
END