* DRIVER ROUTINES FOR SWTPC MF-68
*
* COPYRIGHT (C) 1980 BY
* TECHNICAL SYSTEMS CONSULTANTS, INC.
* 111 PROVIDENCE RD, CHAPEL HILL, NC 27514
*
* THESE DRIVERS ARE FOR A SINGLE-SIDED, SINGLE
* DENSITY SWTPC MF-68 MINIFLOPPY DISK SYSTEM.
*
* THE DRIVER ROUTINES PERFORM THE FOLLOWING
* 1. READ SINGLE SECTOR - DREAD
* 2. WRITE SINGLE SECTOR - DWRITE
* 3. VERIFY WRITE OPERATION - VERIFY
* 4. RESTORE HEAD TO TRACK 00 - RESTOR
* 5. DRIVE SELECTION - DRIVE
* 6. CHECK READY - DCHECK
* 7. QUICK CHECK READY - DQUICK
* 8. DRIVER INITIALIZATION - DINIT
* 9. WARM START ROUTINE - DWARM
* 10. SEEK ROUTINE - DSEEK
*EQUATES
0002 DRQ EQU 2 DRQ BIT MASK
0001 BUSY EQU 1 BUSY MASK
001C RDMSK EQU $1C READ ERROR MASK
0018 VERMSK EQU $18 VERIFY ERROR MASK
005C WTMSK EQU $5C WRITE ERROR MASK
E014 DRVREG EQU $E014 DRIVE REGISTER
E018 COMREG EQU $E018 COMMMAND REGISTER
E019 TRKREG EQU $E019 TRACK REGISTER
E01A SECREG EQU $E01A SECTOR REGISTER
E01B DATREG EQU $E01B DATA REGISTER
008C RDCMND EQU $8C READ COMMAND
00AC WTCMND EQU $AC WRITE COMMAND
000B RSCMND EQU $0B RESTORE COMMAND
001B SKCMND EQU $1B SEEK COMMAND
CC34 PRCNT EQU $CC34
***********************************************
* DISK DRIVER ROUTINE JUMP TABLE
***********************************************
DE00 ORG $DE00
DE00 7E DE2E DREAD JMP READ
DE03 7E DE8E DWRITE JMP WRITE
DE06 7E DEC1 DVERFY JMP VERIFY
DE09 7E DEDA RESTOR JMP RST
DE0C 7E DEED DRIVE JMP DRV
DE0F 7E DF10 DCHECK JMP CHKRDY
DE12 7E DF10 DQUICK JMP CHKRDY
DE15 7E DE23 DINIT JMP INIT
DE18 7E DE2D DWARM JMP WARM
DE1B 7E DE71 DSEEK JMP SEEK
***********************************************
Page 81 - Appendix G
6809 FLEX Adaptation Guide Sample Disk Drivers
* GLOBAL VARIABLE STORAGE
DE1E 00 CURDRV FCB 0 CURRENT DRIVE
DE1F 0000 0000 DRVTRK FDB 0,0 CURRENT TRACK PER DRIVE
* INIT AND WARM
*
* DRIVER INITIALIZATION
DE23 BE DE1E INIT LDX #CURDRV POINT TO VARIABLES
DE26 C6 05 LDB #5 NO. OF BYTES TO CLEAR
DE28 6F 80 INIT2 CLR 0,X+ CLEAR THE STORAGE
DE2A 5A DECB
DE20 26 FB BNE INIT2 LOOP TIL DONE
DE2D 39 WARM RTS WARM START NOT NEEDED
* READ
*
* READ ONE SECTOR
DE2E 8D 41 READ BSR SEEK SEEK TO TRACK
DE30 86 8C LDA #RDCMND SETUP READ SECTOR COMMAND
DE32 7D CC34 TST PRCNT ARE WE SPOOLING?
DE35 27 03 BEQ READ2 SKIP IF NOT
DE37 113F SWI3 ELSE, SWITCH TASKS
DE39 12 NOP NECESSARY FOR SBUG
DE3A 1A 10 READ2 SEI DISABLE INTERRUPTS
DE3C B7 E018 STA COMREG ISSUE READ COMMAND
DE3F 17 00E5 LBSR DEL28 DELAY
DE42 5F CLRB GET SECTOR LENGTH (=256)
DE43 B6 E018 READ3 LDA COMREG GET WD STATUS
DE46 85 02 BITA #DRQ CHECK FOR DATA
DE48 26 08 BNE READ5 BRANCH IF DATA PRESENT
DE4A 85 01 BITA #BUSY CHECK IF BUSY
DE4C 26 F5 BNE READ3 LOOP IF SO
DE4E 1F 89 TFR A,B ERROR IF NOT
DE50 20 0A BRA READ6
DE52 B6 E01B READ5 LDA DATREG GET DATA BYTE
DE55 A7 80 STA 0,X+ PUT IN MEMORY
DE57 5A DECB DEC THE COUNTER
DE58 26 E9 BNE READ3 LOOP TIL DONE
DE5A 8D 05 BSR WAIT WAIT TIL WD IS FINISHED
DE5C C5 1C READ6 BITB #RDMSK MASK ERRORS
DE5E 1C EF CLI ENABLE INTERRUPTS
DE60 39 RTS RETURN
* WAIT
*
* WAIT FOR 1771 TO FINISH COMMAND
DE61 7D CC34 WAIT TST PRCNT ARE WE SPOOLING?
DE64 27 03 BEQ WAIT1 SKIP IF NOT
Page 82 - Appendix G
Sample Disk Drivers 6809 FLEX Adaptation Guide
DE66 113F SWI3 SWITCH TASKS IF SO
DE68 12 NOP NECESSARY FOR SBUG
DE69 F6 E018 WAIT1 LDB COMREG GET WD STATUS
DE6C C5 01 BITB #BUSY CHECK IF BUSY
DE6E 26 F1 BNE WAIT LOOP TIL NOT BUSY
DE70 39 RTS RETURN
* SEEK
*
* SEEK THE SPECIFIED TRACK
DE71 F7 E0 1A SEEK STB SECREG SET SECTOR
DE74 Bl E019 CMPA TRKREG DIF THAN LAST?
DE77 27 12 BEQ SEEK4 EXIT IF NOT
DE79 B7 E01B STA DATREG SET NEW WD TRACK
DE7C 17 00A8 LBSR DEL28 GO DELAY
DE7F 86 1B LDA #SKCMND SETUP SEEK COMMAND
DE81 B7 E018 STA COMREG ISSUE SEEK COMMAND
DE84 17 00A0 LBSR DEL28 GO DELAY
DE87 8D D8 BSR WAIT WAIT TIL DONE
DE89 C5 10 BITB #$10 CHECK FOR SEEK ERROR
DE8B 16 0099 SEEK4 LBRA DEL28 DELAY
* WRITE
*
* WRITE ONE SECTOR
DE8E 8D El WRITE BSR SEEK SEEK TO TRACK
DE90 86 AC LDA #WTDMND SETUP WRITE SCTR COMMAND
DE92 7D CC34 TST PRCNT ARE WE SPOOLING?
DE95 27 03 BEQ WRITE2 SKIP IF NOT
DE97 113F SWI3 CHANGE TASKS IF SO
DE99 12 NOP NECESSARY FOR SBUG
DE9A 1A 10 WRITE2 SEI DISABLE INTERRUPTS
DE9C B7 E018 STA COMREG ISSUE WRITE COMMAND
DE9F 17 0085 LBSR DEL28 DELAY
DEA2 5F CLRB GET SECTOR LENGTH (=256)
DEA3 B6 E018 WRITE3 LDA COMREG CHECK WD STATUS
DEA6 85 02 BITA #DRQ READY FOR DATA?
DEA8 26 08 BNE WRITE5 SKIP IF READY
DEAA 85 01 BITA #BUSY STILL BUSY?
DEAC 26 F5 BNE WRITE3 LOOP IF SO
DEAE 1F 89 TFR A,B ERROR IF NOT
DEB0 20 0A BRA WRITE6
DEB2 A6 80 WRITE5 LDA 0,X+ GET A DATA BYTE
DEB4 B7 E01B STA DATREG SEND TO DISK
DEB7 5A DECB DEC THE COUNT
DEB8 26 E9 BNE WRITE3 FINISHED?
DEBA 8D A5 BSR WAIT WAIT TIL WD IS FINISHED
DEBC C5 5C WRITE6 BITB #WTMSK MASK ERRORS
DEBE 1C EF CLI ENABLE INTERRUPTS
DEC0 39 RTS RETURN
* VERIFY
Page 83 - Appendix G
6809 FLEX Adaptation Guide Sample Disk Drivers
*
* VERIFY LAST SECTOR WRITTEN
DEC1 86 8C VERIFY LDA #RDCMND SETUP VERIFY COMMAND
DEC3 7D CC34 TST PRCNT ARE WE SPOOLING?
DEC6 27 03 BEQ VERIF2 SKIP IF NOT
DEC8 113F SWI3 CHANGE TASKS IF SO
DECA 12 NOP NECESSARY FOR SBUG
DECB 1A 10 VERIF2 SEI DISABLE INTERRUPTS
DECD B7 E018 STA COMREG ISSUE VERIFY COMMAND
DED0 17 0054 LBSR DEL28 GO DELAY
DED3 8D 8C BSR WAIT WAIT TIL WD IS DONE
DED5 1C EF CLI ENABLE INTERRUPTS
DED7 C5 18 BITB #VERMSK MASK ERRORS
DED9 39 RTS RETURN
* RST
*
* RST RESTORES THE HEAD TO 00
DEDA 34 10 RST PSHS X SAVE X REGISTER
DEDC 8D 0F BSR DRV DO SELECT
DEDE 86 0B LDA #RSCMND SETUP RESTORE COMMAND
DEE0 B7 E018 STA COMREG ISSUE RESTORE COMMAND
DEE3 8D 42 BSR DEL28 DELAY
DEE5 17 FF79 LBSR WAIT WAIT TIL WD IS FINISHED
DEE8 35 10 PULS X RESTORE POINTER
DEEA C5 D8 BITB #$D8 CHECK FOR ERROR
DEEC 39 RTS RETURN
* DRV
*
* SELECT THE SPECIFIED DRIVE
DEED A6 03 DRV LDA 3,X GET DRIVE NUMBER
DEEF 81 03 CMPA #3 ENSURE IT'S < 4
DEF1 23 05 BLS DRV2 BRANCH IF OK
DEF3 C6 0F LDB #$0F ELSE SET ERROR VALUE
DEF5 1A 01 SEC
DEF7 39 RTS
DEF8 8D 25 DRV2 BSR FNDTRK FIND TRACK
DEFA F6 E019 LDB TRKREG GET CURRENT TRACK
DEFD E7 84 STB 0,X SAVE IT
DEFF B7 E014 STA DRVREG SET NEW DRIVE
DF02 B7 DE1E STA CURDRV
DF05 8D 18 BSR FNDTRK FIND NEW TRACK
DF07 A6 84 LDA 0,X
DF09 B7 E019 STA TRKREG PUT NEW TRACK IN WD
DF0C 8D 19 BSR DEL28 DELAY
DF0E 20 0B BRA OK
Page 84 - Appendix G
Sample Disk Drivers 6809 FLEX Adaptation Guide
* CHKRDY
*
* CHECK DRIVE READY ROUTINE
DF10 A6 03 CHKRDY LDA 3,X GET DRIVE NUMBER
DF12 81 01 CMPA #1 BE SURE IT'S 0 OR 1
DF14 23 05 BLS OK BRANCH IF OK
DF16 C6 80 LDB #$80 ELSE, SHOW NOT READY
DF18 1A 01 SEC
DFLA 39 RTS RETURN
DF1B 5F OK CLRB SHOW NO ERROR
DF1C 1C FE CLC
DFLE 39 RTS
* FIND THE TRACK FOR CURRENT DRIVE
DF1F 8E DE1F FNDTRK LDX #DRVTRK POINT TO TRACK STORE
DF22 F6 DE1E LDB CURDRV GET CURRENT DRIVE
DF25 3A ABX POINT TO DRIVE'S TRACK
DF26 39 RTS RETURN
* DELAY
DF27 17 0000 DEL28 LBSR DEL14
DF2A 17 0000 DEL14 LBSR DEL
DF2D 39 DEL RTS
END